#include <WiFi.h>
#include <ESPAsyncWebServer.h>
#include <ESP32Servo.h>
#include <ESPmDNS.h>

#define MQ2_PIN 34
#define R_L 10.0 
float R0 = 10.0; 

const char* ssid = "Airtel_Sriram";
const char* password = "Skl030615";
IPAddress local_IP(192, 168, 1, 100);
IPAddress gateway(192, 168, 1, 1);
IPAddress subnet(255, 255, 255, 0);

int obs_dist = 15;

// Motor Pins
#define MOTOR1_A 14
#define MOTOR1_B 27
#define MOTOR2_A 26
#define MOTOR2_B 25

// Servo Pins
#define SERVO_PAN  2
#define SERVO_TILT 4

#define LED_PIN 32

// Ultrasonic Sensor Pins
#define TRIG_PIN 5
#define ECHO_PIN 18

Servo panServo;
Servo tiltServo;
AsyncWebServer server(80);

int lastX = 0, lastY = 0;  
int lastPan = 90, lastTilt = 90;
bool autonomousMode = false;

float getGasPPM() {
    int sensorValue = analogRead(MQ2_PIN);
    float voltage = sensorValue * (3.3 / 4095.0);  // ESP32 has 12-bit ADC (0-4095)
    float RS = R_L * ((3.3 - voltage) / voltage);
    
    // Example for LPG: PPM = 1000 * (RS/R0)^(-2.2) (from datasheet)
    float PPM = 1000 * pow((RS / R0), -2.2);
    return PPM;
}



void setup() {
  
     pinMode(MQ2_PIN, INPUT);
         pinMode(LED_PIN, OUTPUT);
    digitalWrite(LED_PIN, LOW); 

    Serial.begin(115200);
    WiFi.config(local_IP, gateway, subnet);
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.println("Connecting to WiFi...");
    }
    Serial.print("ESP32 IP Address: ");
    Serial.println(WiFi.localIP());

    if (!MDNS.begin("esp32")) {
        Serial.println("Error starting mDNS");
    }

    pinMode(MOTOR1_A, OUTPUT);
    pinMode(MOTOR1_B, OUTPUT);
    pinMode(MOTOR2_A, OUTPUT);
    pinMode(MOTOR2_B, OUTPUT);
    pinMode(TRIG_PIN, OUTPUT);
    pinMode(ECHO_PIN, INPUT);

    panServo.attach(SERVO_PAN);
    tiltServo.attach(SERVO_TILT);
    panServo.write(90);
    tiltServo.write(90);

    server.on("/move", HTTP_GET, [](AsyncWebServerRequest *request) {
        if (request->hasParam("x") && request->hasParam("y")) {
            int x = request->getParam("x")->value().toInt();
            int y = request->getParam("y")->value().toInt();
            if (!autonomousMode && (x != lastX || y != lastY)) {
                lastX = x;
                lastY = y;
                controlMotors(x, y);
            }
        }
        request->send(200, "text/plain", "OK");
    });

    server.on("/servo", HTTP_GET, [](AsyncWebServerRequest *request) {
        if (request->hasParam("pan") && request->hasParam("tilt")) {
            int pan = constrain(request->getParam("pan")->value().toInt(), 0, 180);
            int tilt = constrain(request->getParam("tilt")->value().toInt(), 0, 180);
            if (!autonomousMode && (pan != lastPan || tilt != lastTilt)) {
                lastPan = pan;
                lastTilt = tilt;
                panServo.write(pan);
                tiltServo.write(tilt);
                Serial.printf("Updated Pan: %d, Tilt: %d\n", pan, tilt);
            }
        }
        request->send(200, "text/plain", "Servo Updated");
    });

    server.on("/autonomous", HTTP_GET, [](AsyncWebServerRequest *request) {
    if (request->hasParam("state")) {
        autonomousMode = request->getParam("state")->value().toInt() == 1;
        Serial.printf("Autonomous Mode: %s\n", autonomousMode ? "ON" : "OFF");
        if (!autonomousMode) {
            controlMotors(0, 0);  // Stop motors when turning off autonomous mode
        }
    }

    AsyncWebServerResponse *response = request->beginResponse(200, "text/plain", "Autonomous Mode Updated");
    response->addHeader("Access-Control-Allow-Origin", "*");  // Allow requests from any origin
    response->addHeader("Access-Control-Allow-Methods", "GET, POST, OPTIONS");
    response->addHeader("Access-Control-Allow-Headers", "Content-Type");
    request->send(response);
});


    server.on("/airquality", HTTP_GET, [](AsyncWebServerRequest *request) {
        float gasPPM = getGasPPM();
        Serial.printf("Gas PPM: %.2f\n", gasPPM);
        request->send(200, "text/plain", String(gasPPM));
    });

    
server.on("/led/on", HTTP_GET, [](AsyncWebServerRequest *request) {
    digitalWrite(LED_PIN, HIGH); // Turn LED ON

    AsyncWebServerResponse *response = request->beginResponse(200, "text/plain", "LED ON");
    response->addHeader("Access-Control-Allow-Origin", "*");
    response->addHeader("Access-Control-Allow-Methods", "GET, OPTIONS");
    request->send(response);
});

server.on("/led/off", HTTP_GET, [](AsyncWebServerRequest *request) {
    digitalWrite(LED_PIN, LOW); // Turn LED OFF

    AsyncWebServerResponse *response = request->beginResponse(200, "text/plain", "LED OFF");
    response->addHeader("Access-Control-Allow-Origin", "*");
    response->addHeader("Access-Control-Allow-Methods", "GET, OPTIONS");
    request->send(response);
});



    server.begin();
}

void controlMotors(int x, int y) {
    Serial.printf("Move Command - X: %d, Y: %d\n", x, y);
    if (y == 1) {  // Forward
        digitalWrite(MOTOR1_A, HIGH);
        digitalWrite(MOTOR1_B, LOW);
        digitalWrite(MOTOR2_A, HIGH);
        digitalWrite(MOTOR2_B, LOW);
    } else if (y == -1) {  // Backward
        digitalWrite(MOTOR1_A, LOW);
        digitalWrite(MOTOR1_B, HIGH);
        digitalWrite(MOTOR2_A, LOW);
        digitalWrite(MOTOR2_B, HIGH);
    } else if (x == -1) {  // Left
        digitalWrite(MOTOR1_A, LOW);
        digitalWrite(MOTOR1_B, HIGH);
        digitalWrite(MOTOR2_A, HIGH);
        digitalWrite(MOTOR2_B, LOW);
    } else if (x == 1) {  // Right
        digitalWrite(MOTOR1_A, HIGH);
        digitalWrite(MOTOR1_B, LOW);
        digitalWrite(MOTOR2_A, LOW);
        digitalWrite(MOTOR2_B, HIGH);
    } else {  // Stop
        digitalWrite(MOTOR1_A, LOW);
        digitalWrite(MOTOR1_B, LOW);
        digitalWrite(MOTOR2_A, LOW);
        digitalWrite(MOTOR2_B, LOW);
    }
}

float getDistance() {
    digitalWrite(TRIG_PIN, LOW);
    delayMicroseconds(2);
    digitalWrite(TRIG_PIN, HIGH);
    delayMicroseconds(10);
    digitalWrite(TRIG_PIN, LOW);
    float duration = pulseIn(ECHO_PIN, HIGH);
    return duration * 0.034 / 2;  // Convert to cm
}

void runAutonomousMode() {
    if (!autonomousMode) return;

    controlMotors(0, 1);  // Move forward
    while (autonomousMode) {
        float frontDist = getDistance();
        Serial.printf("Front Distance: %.2f cm\n", frontDist);

        if (frontDist < obs_dist) {  // Obstacle detected
            controlMotors(0, 0);  // Stop
            delay(500);

            // Scan Left and Right
            panServo.write(30);  // Look Left
            tiltServo.write(90);  // Look Left
            delay(500);
            float leftDist = getDistance();
            Serial.printf("Left Distance: %.2f cm\n", leftDist);

            panServo.write(150);  // Look Right
            tiltServo.write(90);  // Look Right
            delay(500);
            float rightDist = getDistance();
            Serial.printf("Right Distance: %.2f cm\n", rightDist);

            // panServo.write(90);  // Reset

            if (rightDist > leftDist) {
                panServo.write(150);  // Look Right
                tiltServo.write(90);  // Look Right
                while(obs_dist + 5 >= getDistance()){                  
                  controlMotors(-1, 0);  // Turn Right
                  Serial.println("r");
                  delay(500);
                  controlMotors(0, 0);  // Stop
                }
            } else {
                panServo.write(30);  // Look Left
                tiltServo.write(90);  // Look Left
                while(obs_dist + 5 >= getDistance()){                  
                  controlMotors(1, 0);  // Turn Right
                  Serial.println("l");
                  delay(500);
                  controlMotors(0, 0);  // Stop

                }
            }
            delay(1000);
            panServo.write(90);  // Reset
            tiltServo.write(90);
            delay(1000);

            frontDist = getDistance();

            controlMotors(0, 1);  // Continue forward
        }
        delay(100);
    }
}

void loop() {
    if (autonomousMode) {
        runAutonomousMode();
    }
}
